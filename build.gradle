plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
}

group = 'my'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
	implementation 'org.json:json:20231013'
	implementation 'commons-io:commons-io:2.15.1'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	implementation 'org.apache.commons:commons-imaging:1.0.0-alpha5'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('runUnitTests', Test) {
	useJUnitPlatform {
		includeTags("UNIT-TEST")
		excludeTags("INTEGRATION-TEST")
	}
}

tasks.register('runIntegrationTests', Test) {
	useJUnitPlatform {
		includeTags("INTEGRATION-TEST")
		excludeTags("UNIT-TEST")
	}
}

tasks.register('runAllTests', Test) {
	useJUnitPlatform {
		includeTags("INTEGRATION-TEST", "UNIT-TEST")
	}

	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn runAllTests

	reports {
		xml.required = true
		html.required = true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				counter = 'CLASS'
				value = 'COVEREDRATIO'
				minimum = 1.0
			}
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.85
			}
		}
	}
}